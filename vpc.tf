# Create the VPC
resource "aws_vpc" "myapp-vpc" {
    cidr_block           = "10.0.0.0/16"
    enable_dns_hostnames = true
    tags = {
        Name = "myapp-vpc"
        }
}

# Create a public subnet
resource "aws_subnet" "public" {
    count                   = 2
    vpc_id                  = aws_vpc.myapp-vpc.id
    cidr_block              = cidrsubnet(aws_vpc.myapp-vpc.cidr_block, 8, count.index)
    availability_zone       = data.aws_availability_zones.available.names[count.index]
    map_public_ip_on_launch = true # Instances launched into this subnet get a public IP
    
    tags = {
        Name = "PublicSubnet_${count.index}"
        }
        
}


# Create a private subnet
resource "aws_subnet" "private" {
    count                   = 2
    vpc_id                  = aws_vpc.myapp-vpc.id
    cidr_block              = cidrsubnet(aws_vpc.myapp-vpc.cidr_block, 8, count.index + 2)  #(The +2 here means it will start using cidr creation from 2, this is becaause 0 and 1 has been used by public subnet e.g 10.0.2.0/24,  10.0.3.0/24 )
    availability_zone       = data.aws_availability_zones.available.names[count.index]
    map_public_ip_on_launch = false # Instances launched into this subnet get a private IP
    
    tags = {
        Name = "PrivateSubnet_${count.index}"
        }
        
}


# Create an Internet Gateway (to route public subnet)
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.myapp-vpc.id

  tags = {
    Name = "main-igw"
  }
}



# Create Elastic IPs for the NAT gateways (this is the public ip assigned to the nat gatway which the private subnet will mount on, inother to access the internet)
resource "aws_eip" "nat" {
  count  = 2 # One EIP per NAT Gateway
  domain = "vpc"
}


# Create NAT gateways (You have to create 2 nat gways to route private subnet as each subnet can only mount a single nat gway and not a single nat on multiple subnet)
resource "aws_nat_gateway" "nat" {
  count         = 2                     #The two counts means for the 2 private subnets created
  allocation_id = aws_eip.nat[count.index].id
  subnet_id     = aws_subnet.public[count.index].id # NAT GW lives in a public subnet

  tags = {
    Name = "nat-gateway-${count.index}" #The +1 mean it will start its count from 1
  }
}



# Create a Public Route Table (We ony need a single route table in a public subnet. That is because multiple subnets can mount on a single route table)
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.myapp-vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "public-route-table"
  }
}

# Associate Public Subnets with Public Route Table #This means i am assigning 1 route table to each public subnet
resource "aws_route_table_association" "public" {
  count          = 2
  subnet_id      = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public.id
}


# Create a Private Route Table for each AZ (a private subnet needs a route table per subnet. This is because of nat gateway which is only applicabe to a subnet)
resource "aws_route_table" "private" {
  count  = 2
  vpc_id = aws_vpc.myapp-vpc.id
  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat[count.index].id # Route traffic through the respective NAT GW
  }

  tags = {
    Name = "private-route-table-${count.index}"
  }
}

# Associate Private Subnets with Private Route Tables
resource "aws_route_table_association" "private" {
  count          = 2
  subnet_id      = aws_subnet.private[count.index].id
  route_table_id = aws_route_table.private[count.index].id
}